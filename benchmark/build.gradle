import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.example.benchmark'
    compileSdk 33

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }

    defaultConfig {
        minSdk 28
        targetSdk 33
        missingDimensionStrategy "default", "product"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
//        testInstrumentationRunnerArguments["androidx.benchmark.enabledRules"] = "BaselineProfile"
    }

    buildTypes {
        // This benchmark buildType is used for benchmarking, and should function like your
        // release build (for example, with minification on). It's signed with a debug key
        // for easy local/CI testing.
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            matchingFallbacks = ["debug"]
        }
    }
    flavorDimensions += ["default"]
    productFlavors {
        develop { dimension = "default" }
        product { dimension = "default" }
    }

    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true
    testOptions {
        managedDevices {
            devices {
                // なるべく本番に近いビルドを試す必要があるが、現状そこまでbaseline-profile.txtに差が出るとは思えないのでdevelopBenchmarkを指定。developBenchmarkは難読化されたdevelopDebugという位置付け。
                // $ ./gradlew :benchmark:pixel6ProApi31DevelopBenchmarkAndroidTest -P android.testInstrumentationRunnerArguments.androidx.benchmark.enabledRules=BaselineProfile
                // 出力先フォルダ:
                // benchmark/build/outputs/managed_device_android_test_additional_output/flavors/develop/pixel6ProApi31/
                // or benchmark/build/intermediates/managed_device_android_test_additional_output/developBenchmark/pixel6ProApi31UncheckedStagingBenchmarkAndroidTest/
                // develop
                pixel6ProApi31(ManagedVirtualDevice) {
                    device = "Pixel 6 Pro" // Pixel 6はダメだった（2023/05/18）
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.2.0-alpha14'
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == "benchmark"
    }
}